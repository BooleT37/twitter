buildscript {
	ext {
		springBootVersion = '1.4.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'spring-boot'

jar {
	baseName = 'twitter'
	version = '0.0.1-SNAPSHOT'
}
//sourceCompatibility = 1.8
//targetCompatibility = 1.8

repositories {
	mavenCentral()
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('com.h2database:h2')
	compile('org.apache.tomcat.embed:tomcat-embed-jasper')
	compile('javax.servlet:jstl:1.2')
	compile group: 'javax.inject', name: 'javax.inject', version: '1'
	testCompile('org.mockito:mockito-core:1.10.19')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	integrationTestCompile 'io.rest-assured:rest-assured:3.0.1'
	integrationTestCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
    compile('org.springframework.security:spring-security-web')
    compile('org.springframework.security:spring-security-config')
    compile('org.springframework.security:spring-security-test')
}
